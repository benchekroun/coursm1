# commentaire

pred init(var2 Xlaitue, var2 Xchevre, var2 Xloup, var2 Xfermier) = 
     0 notin Xlaitue &
     0 notin Xchevre & 
     0 notin Xloup &
     0 notin Xfermier;

pred fin(var1 t, var2 Xlaitue, var2 Xchevre, var2 Xloup, var2 Xfermier) =
     t in Xlaitue &
     t in Xchevre &
     t in Xloup &
     t in Xfermier;

pred situationGeographiqueCorrecte(var2 X, var2 Y, var2 Xfermier) =
     all1 x:(((x in X & x in Y) <=> x in Xfermier) | ((x notin X & x notin Y) <=> x notin Xfermier));

pred pasDeSituationInterdite(var2 Xlaitue, var2 Xchevre, var2 Xloup, var2 Xfermier) = 
     situationGeographiqueCorrecte(Xlaitue,Xchevre,Xfermier) &
     situationGeographiqueCorrecte(Xchevre,Xloup,Xfermier);

     
pred fermierEtUnIndividu(var1 t, var2 X, var2 Y, var2 Z, var2 Xfermier) =
     # mouvement ouest-est du fermier indiv
     ((((t in Xfermier) & ((t-1) notin Xfermier)) & ((t in X) & ((t-1) notin X)))| 
     # mouvement contraire
     (((t notin Xfermier) & ((t-1) in Xfermier)) & ((t notin X) & ((t-1) in X)))) &
     #ne bouge pas
     (((t in Y) <=> ((t-1) in Y))) & 
     (((t in Z) <=> ((t-1) in Z))) ;

pred fermierSeul(var1 t, var2 Xlaitue, var2 Xchevre, var2 Xloup, var2 Xfermier) =
     # on s'assure que le fermier a bien bouge , et qu'il n'etais pas avec un individu
     (((t in Xfermier) & ((t-1) notin Xfermier)) | ((t notin Xfermier) & ((t-1) in Xfermier))) &  
      (((t in Xlaitue) <=> ((t-1) in Xlaitue))) &
 (((t in Xchevre) <=> ((t-1) in Xchevre))) &
 (((t in Xloup) <=> ((t-1) in Xloup)))   
; 


pred bouger(var1 t, var2 X) = 
     ((t in X)=>((t-1) notin X)) | ((t notin X)=>((t-1)in X));

 pred voyageCorrect(var1 t, var2 Xlaitue, var2 Xchevre, var2 Xloup, var2 Xfermier) = 
      fermierSeul(t,Xlaitue,Xchevre,Xloup,Xfermier)|
      fermierEtUnIndividu(t,Xlaitue,Xchevre,Xloup,Xfermier)|
     fermierEtUnIndividu(t,Xchevre,Xlaitue,Xloup,Xfermier)|
     fermierEtUnIndividu(t,Xloup,Xchevre,Xlaitue,Xfermier);

pred laitueChevreLoup(var2 Xlaitue, var2 Xchevre, var2 Xloup, var2 Xfermier) =
     init(Xlaitue,Xchevre,Xloup,Xfermier) & 
     ex1 t:(fin(t,Xlaitue,Xchevre,Xloup,Xfermier) &
     pasDeSituationInterdite(Xlaitue,Xchevre,Xloup,Xfermier) &
     all1 y:(((y>0)&(y<=t))=>voyageCorrect(y,Xlaitue,Xchevre,Xloup,Xfermier) ));

var2 Xlaitue,Xchevre,Xloup,Xfermier;

laitueChevreLoup(Xlaitue,Xchevre,Xloup,Xfermier);
